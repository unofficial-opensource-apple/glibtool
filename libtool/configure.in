## Process this file with autoconf to create configure.

AC_INIT(ltmain.in)

AM_INIT_AUTOMAKE(libtool, 1.4.2)

# This is a sanity check so we can see which version is used in bug reports.
# It is assumed that we only want to see the date extension for cvs libtool
# versions (i.e. "odd" letters) and not actual alpha releases.
TIMESTAMP=
case "$VERSION" in
  [*[acegikmoqsuwy])]
    TIMESTAMP=`${CONFIG_SHELL} ${srcdir}/mkstamp < ${srcdir}/ChangeLog`
    banner="Configuring $PACKAGE $VERSION$TIMESTAMP"
    dashes=`echo "$banner" | sed 's/./-/g'`

    # Display an obvious version banner
    echo
    echo $dashes
    echo "$banner"
    echo $dashes
    echo
    ;;
esac
AC_SUBST(TIMESTAMP)

pkgdatadir='${datadir}'"/${PACKAGE}"
AC_SUBST(pkgdatadir) # automake does not need this, but libtoolize does
aclocaldir='${datadir}/aclocal'
AC_SUBST(aclocaldir)

# Use the specified CC and LD
AC_PROG_CC
AM_PROG_LD
AC_SUBST(LD)
AM_PROG_NM
AC_SUBST(NM)
AC_PROG_LN_S

# Declare win32 dll support
AC_LIBTOOL_WIN32_DLL

# Check for dlopen support
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

AC_ARG_ENABLE(ltdl-install,
[  --disable-ltdl-install  do not install libltdl])
if test x"${enable_ltdl_install+set}" != xset; then
  enable_ltdl_install=yes
  ac_configure_args="$ac_configure_args --enable-ltdl-install"
fi
AC_CONFIG_SUBDIRS(libltdl)

# all subdirectories that are configured on demand, but that must be
# included in the distribution
CONF_SUBDIRS="cdemo demo depdemo mdemo"
AC_SUBST(CONF_SUBDIRS)

ACINCLUDE_M4_LIST="${srcdir}/acinclude.m4"
DIST_MAKEFILE_LIST=
for dir in $CONF_SUBDIRS; do
  ACINCLUDE_M4_LIST="$ACINCLUDE_M4_LIST ${srcdir}/$dir/acinclude.m4"
  DIST_MAKEFILE_LIST="$DIST_MAKEFILE_LIST$dir/Makefile "
done
AC_SUBST(ACINCLUDE_M4_LIST)
AC_SUBST(DIST_MAKEFILE_LIST)

AC_OUTPUT([Makefile doc/Makefile tests/Makefile])
