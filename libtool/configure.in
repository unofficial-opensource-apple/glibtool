dnl Process this file with autoconf to create configure.

AC_INIT(ltmain.in)

AM_INIT_AUTOMAKE(libtool, 1.3.5)

# This is a sanity check so we can see which version is used in bug reports.
# It is assumed that we only want to see the date extension for cvs libtool
# versions (i.e. "odd" letters) and not actual alpha releases.
case "$VERSION" in
changequote(,)
  *[acegikmoqsuwy]) 
changequote([,])
    TIMESTAMP=`${CONFIG_SHELL} ${srcdir}/mkstamp < ${srcdir}/ChangeLog`
    banner="Configuring $PACKAGE $VERSION$TIMESTAMP"
    dashes=`echo "$banner" | sed 's/./-/g'`

    # Display an obvious version banner
    echo
    echo $dashes
    echo "$banner"
    echo $dashes
    echo
    ;;
esac    

pkgdatadir='${datadir}'"/${PACKAGE}"
AC_SUBST(pkgdatadir) dnl automake does not need this, but libtoolize does
aclocaldir='${datadir}/aclocal'
AC_SUBST(aclocaldir)

dnl Use the specified CC, RANLIB, and LD while running ltconfig.
AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_LD
AC_SUBST(LD)
AM_PROG_NM
AC_SUBST(NM)
AC_PROG_LN_S

dnl For the `lineno' script (which puts line numbers into `ltconfig').
AC_PROG_AWK

dnl Check for dlopen support
AC_LIBTOOL_DLOPEN
AC_LIBTOOL_SETUP
AC_SUBST(DLLTOOL)
AC_SUBST(FILE)
AC_SUBST(OBJDUMP)
AC_SUBST(AS)

LIBTOOL_FLAGS="$libtool_flags"
AC_SUBST(LIBTOOL_FLAGS)

AC_ARG_ENABLE(ltdl-install,
[  --disable-ltdl-install  do not install libltdl])
if test x"${enable_ltdl_install+set}" != xset; then
  enable_ltdl_install=yes
  ac_configure_args="$ac_configure_args --enable-ltdl-install"
fi
AC_CONFIG_SUBDIRS(libltdl)

dnl all subdirectories that are configured on demand, but that must be
dnl included in the distribution
CONF_SUBDIRS="cdemo demo mdemo"
AC_SUBST(CONF_SUBDIRS)

ACINCLUDE_M4_LIST="${srcdir}/acinclude.m4 ${srcdir}/libltdl/acinclude.m4 "
DIST_MAKEFILE_LIST=
for dir in $CONF_SUBDIRS; do
  ACINCLUDE_M4_LIST="$ACINCLUDE_M4_LIST${srcdir}/$dir/acinclude.m4 "
  DIST_MAKEFILE_LIST="$DIST_MAKEFILE_LIST$dir/Makefile "
done
AC_SUBST(ACINCLUDE_M4_LIST)
AC_SUBST(DIST_MAKEFILE_LIST)

AC_OUTPUT_COMMANDS([
  test -f ${saved_top_srcdir}/ltconfig || ${MAKE-make} ${saved_top_srcdir}/ltconfig
  test -f ${saved_top_srcdir}/ltmain.sh || ${MAKE-make} ${saved_top_srcdir}/ltmain.sh
], [saved_top_srcdir=${srcdir}])
AC_OUTPUT([Makefile doc/Makefile tests/Makefile])

# Local Variables:
# mode:shell-script
# sh-indentation:2
# End:
